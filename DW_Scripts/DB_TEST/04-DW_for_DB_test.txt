

/****************Datawarhouse script for the DB Test**********/

USE [GreenhouseSystemDBTest]
GO

/****** Object:  Schema [stage]    Script Date: 05/12/2022 10.49.03 ******/
CREATE SCHEMA [stage]
GO
CREATE SCHEMA [edw]
GO
CREATE SCHEMA [etl]
GO

/*************************************************************/

/*********************** Create Dimensions and fact table in the STAGE ************************************/


USE [GreenhouseSystemDBTest]
GO


CREATE TABLE stage.DimGreenhouse (
 Greenhouse_id BIGINT NOT NULL,
 GreenhouseName [nvarchar](max),
 PlantProfileName [nvarchar](max),
 Description [nvarchar](max),
 OptimalTemperature [real],
 OptimalHumidity [real],
 OptimalCo2 [real],
 OptimalLight INT,
 TemperatureMax [real],
 TemperatureMin [real],
 HumidityMax [real],
 HumidityMin [real],
 Co2Max [real],
 Co2Min [real]
);

ALTER TABLE stage.DimGreenhouse ADD CONSTRAINT PK_DimGreenhouse PRIMARY KEY (Greenhouse_id);


CREATE TABLE stage.DimUser (
 User_id BIGINT NOT NULL,
 Username [nvarchar](max),
 Email [nvarchar](450)
);

ALTER TABLE stage.DimUser ADD CONSTRAINT PK_DimUser PRIMARY KEY (User_id);


CREATE TABLE stage.FactMeasurement (
 Measurement_id BIGINT NOT NULL,
 Greenhouse_id BIGINT NOT NULL,
 User_id BIGINT NOT NULL,
 Temperature [real],
 Humidity [real],
 Co2 [real],
 Light INT,
 Timestamp DATETIME
);

ALTER TABLE stage.FactMeasurement ADD CONSTRAINT PK_FactMeasurement PRIMARY KEY (Measurement_id,Greenhouse_id,User_id);


ALTER TABLE stage.FactMeasurement ADD CONSTRAINT FK_FactMeasurement_0 FOREIGN KEY (Greenhouse_id) REFERENCES stage.DimGreenhouse (Greenhouse_id);
ALTER TABLE stage.FactMeasurement ADD CONSTRAINT FK_FactMeasurement_1 FOREIGN KEY (User_id) REFERENCES stage.DimUser (User_id);

/*********************** Create Dimensions and fact table in the EDW ************************************/


USE [GreenhouseSystemDBTest]
GO

CREATE TABLE edw.DimDate (
 D_ID INT NOT NULL,
 Date DATE,
 Day INT,
 Month INT,
 Year INT,
 MonthName NCHAR VARYING(9),
 Week INT,
 DayOfWeek INT,
 WeekDayName NCHAR VARYING(9),
 Season NCHAR VARYING(9)
);

ALTER TABLE edw.DimDate ADD CONSTRAINT PK_DimDate PRIMARY KEY (D_ID);


CREATE TABLE edw.DimGreenhouse (
 G_ID BIGINT IDENTITY NOT NULL,
 Greenhouse_id BIGINT,
 GreenhouseName [nvarchar](max),
 PlantProfileName [nvarchar](max),
 Description [nvarchar](max),
 OptimalTemperature [real],
 OptimalHumidity [real],
 OptimalCo2 [real],
 OptimalLight INT,
  TemperatureMax [real],
 TemperatureMin [real],
 HumidityMax [real],
 HumidityMin [real],
 Co2Max [real],
 Co2Min [real],
 ValidFrom INT,
 ValidTo INT
);

ALTER TABLE edw.DimGreenhouse ADD CONSTRAINT PK_DimGreenhouse PRIMARY KEY (G_ID);


CREATE TABLE edw.DimTime (
 T_ID INT IDENTITY NOT NULL,
 Time TIME,
 Hour SMALLINT,
 Minute SMALLINT
);

ALTER TABLE edw.DimTime ADD CONSTRAINT PK_DimTime PRIMARY KEY (T_ID);


CREATE TABLE edw.DimUser (
 U_ID BIGINT IDENTITY NOT NULL,
 User_id BIGINT,
 Username [nvarchar](max),
 Email [nvarchar](450),
 ValidFrom INT,
 ValidTo INT
);

ALTER TABLE edw.DimUser ADD CONSTRAINT PK_DimUser PRIMARY KEY (U_ID);


CREATE TABLE edw.FactMeasurement (
 M_ID BIGINT NOT NULL,
 U_ID BIGINT NOT NULL,
 G_ID BIGINT NOT NULL,
 D_ID INT NOT NULL,
 T_ID INT NOT NULL,
 Temperature [real],
 Humidity [real],
 Co2 [real],
 Light INT
);

ALTER TABLE edw.FactMeasurement ADD CONSTRAINT PK_FactMeasurement PRIMARY KEY (M_ID,U_ID,G_ID,D_ID,T_ID);


ALTER TABLE edw.FactMeasurement ADD CONSTRAINT FK_FactMeasurement_0 FOREIGN KEY (U_ID) REFERENCES edw.DimUser (U_ID);
ALTER TABLE edw.FactMeasurement ADD CONSTRAINT FK_FactMeasurement_1 FOREIGN KEY (G_ID) REFERENCES edw.DimGreenhouse (G_ID);
ALTER TABLE edw.FactMeasurement ADD CONSTRAINT FK_FactMeasurement_2 FOREIGN KEY (D_ID) REFERENCES edw.DimDate (D_ID);
ALTER TABLE edw.FactMeasurement ADD CONSTRAINT FK_FactMeasurement_3 FOREIGN KEY (T_ID) REFERENCES edw.DimTime (T_ID);


/******* CREATE ETL LOG TABLE*****************************/

Use [GreenhouseSystemDBTest]
GO

-- Create a log update table to keep track of all the recent loads for any dimension
CREATE TABLE [GreenhouseSystemDBTest].[etl].[LogUpdate](
     [Table] [nvarchar] (50) NULL ,
     [LastLoadDate] INT NULL 
     ----,[LastLoadTime] int NULL

) ON [PRIMARY]

GO

Use [GreenhouseSystemDBTest]
GO

-- Create a log update table to keep track of all the recent loads for any dimension
CREATE TABLE [GreenhouseSystemDBTest].[etl].[LogFact](
     [Table] [nvarchar] (50) NULL ,
     [LastLoadDate] DATETIME NULL 
     ----,[LastLoadTime] int NULL

) ON [PRIMARY]

GO
/**********************Dim Time ************************/

use GreenhouseSystemDBTest
GO

DECLARE @StartTime TIME;
Declare @i int;
SET @StartTime= '00:00';
SET @i=0;
WHILE @i<1440
  BEGIN 
   INSERT INTO GreenhouseSystemDBTest.edw.DimTime
                ([Time],[Hour],[Minute])   
           SELECT
		   @StartTime as [Time],
		   DATEPART(HOUR, @StartTime) as [Hour],
		   DATEPART(Minute, @StartTime) as [Minute]	    
  SET @StartTime= DATEADD(MINUTE, 1, @StartTime);
  SET @i+=1;
  END

/******************** DIM Date ************************/

use GreenhouseSystemDBTest
GO

DECLARE @StartDate DATE;
DECLARE @EndDate DATE;

SET @StartDate= '2021-01-01';
SET @EndDate= DATEADD(Year,10,GETDATE());

WHILE @StartDate <= @EndDate
  BEGIN   
   INSERT INTO GreenhouseSystemDBTest.[edw].[DimDate]
           ([D_ID]
           ,[Date]
           ,[Day]
           ,[Month]
           ,[MonthName]
           ,[Week]       
           ,[Year]
           ,[DayOfWeek] 
           ,[WeekDayName]		    
			,[Season])
    SELECT
	       CONVERT(CHAR(8), @StartDate, 112) AS D_ID
		   ,@StartDate as [Date]
		   ,DATEPART(day,@StartDate) as Day
		   ,DATEPART(month,@StartDate) as Month
		   ,DATENAME(month,@StartDate) as MonthName
		   ,DATEPART(week,@StartDate) as Week
		   
		   ,DATEPART(year,@StartDate) as Year
		   ,DATEPART(WEEKDAY,@StartDate) as DayOfWeek
		   ,DATENAME(WEEKDAY,@StartDate) as WeekDayName		  
		   ,  CASE 
        WHEN DATEPART(month,@StartDate)= 12 or DATEPART(month,@StartDate)= 1 or DATEPART(month,@StartDate)= 2   THEN 'Winter'
        WHEN DATEPART(month,@StartDate)= 3 or DATEPART(month,@StartDate)= 4 or DATEPART(month,@StartDate)= 5   THEN 'Spring'
		WHEN DATEPART(month,@StartDate)= 6 or DATEPART(month,@StartDate)= 7 or DATEPART(month,@StartDate)= 8  THEN 'Summer'
        ELSE 'Autumn'
      END
  SET @StartDate= DATEADD(dd, 1 , @StartDate)

END
GO

/************** Populate the stageand clean it **************************/

USE [GreenhouseSystemDBTest]
GO

INSERT INTO [GreenhouseSystemDBTest].[stage].[DimUser]
           ([User_id]
           ,[username]
           ,[email])
    SELECT
	        [id]
           ,[username]
           ,[email]
     FROM [dbo].[Users]

GO

/********** Populate Dim Greenhouse****************/

INSERT INTO [stage].[DimGreenhouse]
           ([Greenhouse_id]
           ,[GreenhouseName]
           ,[PlantProfileName]
           ,[Description]
           ,[OptimalTemperature]
           ,[OptimalHumidity]
           ,[OptimalCo2]
           ,[OptimalLight]
           ,[TemperatureMax]
           ,[TemperatureMin]
           ,[HumidityMax]
           ,[HumidityMin]
           ,[Co2Max]
           ,[Co2Min])
     SELECT  gh.Id
	        ,gh.Name
			,pp.Name
			,Description
			,[OptimalTemperature]
           ,[OptimalHumidity]
           ,[OptimalCo2]
           ,[OptimalLight]
           ,[TemperatureMax]
           ,[TemperatureMin]
           ,[HumidityMax]
           ,[HumidityMin]
           ,[Co2Max]
           ,[Co2Min]
    FROM      [dbo].[GreenHouses] gh
	LEFT JOIN [dbo].[PlantProfiles] pp
	on gh.ActivePlantProfileId=pp.Id
	LEFT JOIN [dbo].[Thresholds] th
	on pp.ThresholdId=th.Id

GO

/*************************Clean the Stage*******************/

USE [GreenhouseSystemDBTest]
GO

UPDATE [stage].[DimGreenhouse]
   SET
      [PlantProfileName] = 'N/Profile'
      ,[Description] = 'N/Description'
      ,[OptimalTemperature] = 0
      ,[OptimalHumidity] = 0
      ,[OptimalCo2] = 0
      ,[OptimalLight] = 0
      ,[TemperatureMax] = 100
      ,[TemperatureMin] = -100
      ,[HumidityMax] = 1000
      ,[HumidityMin] = -1000
      ,[Co2Max] = 10000
      ,[Co2Min] = -10000
 WHERE [PlantProfileName] is null
GO

/**************Populate FACT Measurement***********/


USE [GreenhouseSystemDBTest]
GO

INSERT INTO [stage].[FactMeasurement]
           ([Measurement_id]
           ,[Greenhouse_id]
           ,[User_id]
           ,[Temperature]
           ,[Humidity]
           ,[Co2]
           ,[Light]
           ,[Timestamp])
     SELECT m.Id
	        ,m.GreenhouseId
			,u.User_id
			,[Temperature]
            ,[Humidity]
            ,[Co2]
            ,[Light]
            ,[Timestamp]
	 FROM       [dbo].[Measurements]  m
	 INNER JOIN [dbo].[GreenHouses] gh
	 ON m.GreenhouseId=gh.Id
	 INNER JOIN [GreenhouseSystemDBTest].[stage].[DimUser] u
	 ON gh.UserId=u.User_id

GO

/************* Populate EDW ***************************/


USE [GreenhouseSystemDBTest]
GO

INSERT INTO [edw].[DimGreenhouse]
           ([Greenhouse_id]
           ,[GreenhouseName]
           ,[PlantProfileName]
           ,[Description]
           ,[OptimalTemperature]
           ,[OptimalHumidity]
           ,[OptimalCo2]
           ,[OptimalLight]
           ,[TemperatureMax]
           ,[TemperatureMin]
           ,[HumidityMax]
           ,[HumidityMin]
           ,[Co2Max]
           ,[Co2Min] )
     SELECT 
	        [Greenhouse_id]
           ,[GreenhouseName]
           ,[PlantProfileName]
           ,[Description]
           ,[OptimalTemperature]
           ,[OptimalHumidity]
           ,[OptimalCo2]
           ,[OptimalLight]
           ,[TemperatureMax]
           ,[TemperatureMin]
           ,[HumidityMax]
           ,[HumidityMin]
           ,[Co2Max]
           ,[Co2Min]
     FROM  [stage].[DimGreenhouse]

GO

/***************************************************************************/

USE [GreenhouseSystemDBTest]
GO

INSERT INTO [edw].[DimUser]
           ([User_id]
           ,[Username]
           ,[Email]
           )
     SELECT
	        [User_id]
           ,[Username]
           ,[Email]
     FROM  [stage].[DimUser]

GO
/********************************* EDW FACT TABLE***************************/

USE [GreenhouseSystemDBTest]
GO

INSERT INTO [edw].[FactMeasurement]
           ([M_ID]
           ,[U_ID]
           ,[G_ID]
           ,[D_ID]
           ,[T_ID]
           ,[Temperature]
           ,[Humidity]
           ,[Co2]
           ,[Light])
    SELECT  f.Measurement_id
           ,edwu.[U_ID]
           ,edwg.[G_ID]
           ,edwd.[D_ID]
           ,edwt.[T_ID]
           ,f.[Temperature]
           ,f.[Humidity]
           ,f.[Co2]
           ,f.[Light]	
	FROM [GreenhouseSystemDBTest].[stage].[FactMeasurement] f
	INNER JOIN [GreenhouseSystemDBTest].[edw].[DimUser] edwu
	on f.User_id=edwu.User_id
	INNER JOIN  [GreenhouseSystemDBTest].[edw].[DimGreenhouse] edwg
	on f.Greenhouse_id=edwg.Greenhouse_id
	INNER JOIN [GreenhouseSystemDBTest].[edw].[DimDate] edwd
   on CONVERT(DATE,f.timestamp)=edwd.Date
   INNER JOIN [GreenhouseSystemDBTest].[edw].[DimTime] edwt
   on DATEPART(HOUR, f.timestamp)=edwt.Hour AND DATEPART(MINUTE, f.timestamp)=edwt.Minute 
GO







